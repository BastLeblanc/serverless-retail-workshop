buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.bmuschko:gradle-docker-plugin:3.6.2"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

plugins {
    id 'net.ltgt.apt' version '0.18'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.bmuschko.docker-spring-boot-application"

group = 'fishing.lee'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url = "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
    }
}


ext {
    springCloudVersion = 'Finchley.SR1'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compileOnly("org.springframework:spring-context-indexer:5.1.0.RELEASE")
    compile('com.github.derjust:spring-data-dynamodb:5.0.3')
    compile 'com.google.guava:guava:26.0-jre'

    compile 'de.mkammerer:argon2-jvm:2.5'

    compile("com.amazonaws:aws-xray-recorder-sdk-core")
    compile("com.amazonaws:aws-xray-recorder-sdk-aws-sdk")
    compile("com.amazonaws:aws-xray-recorder-sdk-aws-sdk-instrumentor")

    compile("com.amazonaws.serverless:aws-serverless-java-container-spring:1.1.4")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.cloud:spring-cloud-starter-contract-verifier')

    testCompile('com.amazonaws:DynamoDBLocal:1.11.119')

    compileOnly 'org.projectlombok:lombok:1.18.2'
    apt 'org.projectlombok:lombok:1.18.2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
        mavenBom("com.amazonaws:aws-xray-recorder-sdk-bom:2.0.1")
    }
}

task copyNativeDeps(type: Copy) {
    from(configurations.compile + configurations.testCompile) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into 'build/libs'
}

test {
    dependsOn copyNativeDeps
    systemProperty "java.library.path", 'build/libs'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.*
shadowJar {
    dependencies {
        exclude(dependency('org.apache.tomcat.embed:.*'))
    }

    /*
     * Protip: This makes Spring Boot a lot faster in Lambda
     * and can take around 10 seconds off the loading time.
     */
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories' ]
        mergeStrategy = "append"
    }
}

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "local"
}

